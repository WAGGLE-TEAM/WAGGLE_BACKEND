plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'application'
}

group = 'com.trip'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'com.trip.api'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    bootJar.enabled = false
    jar.enabled = true

    if (name == 'waggle-api') {
        bootJar.enabled = true
    }

    repositories {
        mavenCentral()
    }

    ext {
        snippetsDir = file('./build/generated-snippets')
    }

    dependencies {
        // Spring Boot
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // Spring REST Docs
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    }

    asciidoctor.doFirst {
        delete file('src/main/resources/static/docs')
    }

    javadoc {
        options.addStringOption("locale","ko_KR");
        options.addStringOption("encoding","UTF-8");
        options.addStringOption("charset","UTF-8");
        options.addStringOption("docencoding","UTF-8");
    }

    asciidoctor {
        inputs.dir snippetsDir
        // 다른 Task 에 의존한다는 것을 알리는 것이다.
        // test Task 에 의존함을 알린다.
        // asciidoctor task 를 실행할 때, test task 를 먼저 실행한다.
        dependsOn test
    }

    task copyDocument(type: Copy) {
        dependsOn asciidoctor
        from file("build/docs/asciidoc")
        into file("src/main/resources/static/docs")
    }

    build {
        dependsOn copyDocument
    }

    test {
        useJUnitPlatform()
        outputs.dir snippetsDir
    }
}